import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
}

group 'com.imnotstable.qualitychat'
version '1.0.0'

compileJava.sourceCompatibility = '21'
compileJava.targetCompatibility = '21'
compileJava.options.encoding = 'UTF-8'
jar.enabled = false

repositories {
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/"}
    maven { url "https://oss.sonatype.org/content/groups/public/"}
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'dev.jorel:commandapi-bukkit-shade:9.5.1'

    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'me.clip:placeholderapi:2.11.6'
}

shadowJar {
    relocate 'dev.jorel.commandapi', group + '.commandapi'
    minimize()
    exclude 'META-INF/**', 'LICENSE', 'module-info.class'
    archiveFileName = "${project.name}-${rootProject.version}.jar"
}

build {
    dependsOn shadowJar
}

processResources {
    filter(ReplaceTokens, tokens: [version: rootProject.version])
}
